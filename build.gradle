plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "net.darkhax.curseforgegradle" version "1.0.7"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "com.matthewprenger.cursegradle"

    version = "${rootProject.mod_version}+${rootProject.minecraft_version}-${name}"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    Map<String, Map<String, String>> platformSettings = [
            "fabric": [
                    "curse-id": "363949",
                    "modrinth-id": "eA8SXqWL"
            ],
            "quilt": [
                    "curse-id": "363949",
                    "modrinth-id": "eA8SXqWL"
            ],
            "forge": [
                    "curse-id": "368293",
                    "modrinth-id": "QDNS5oAT"
            ]
    ]

    Map<String, Map<String, Boolean>> platformDependencies = [
            "fabric": [
                    "fabric-api": true
            ],
            "quilt": [
                    "qsl": true
            ],
            "forge": []
    ]

    var env = System.getenv()
    def uploadSettings = platformSettings.get(name)
    def uploadDependencies = platformDependencies.get(name)

    if (uploadSettings != null && uploadDependencies != null) {

        File changeLog = rootProject.file("CHANGELOG.md")
        String changeLogText = changeLog.exists() ? changeLog.text : ""

        if (env.MODRINTH) {
            modrinth {
                token = env.MODRINTH
                versionName = "Repurposed Structures"
                versionNumber = "${project.version}"
                projectId = "QDNS5oAT"
                changelog = changeLogText
                projectId = uploadSettings["modrinth-id"]

                gameVersions = ["${rootProject.minecraft_version}"]
                loaders = [name]

                if (name == "fabric") {
                    versionType = "BETA"
                }
                else {
                    versionType = "RELEASE"
                }

                uploadFile = tasks.remapJar

                dependencies {
                    def optionalContainer = getOptional()
                    def requiredContainer = getRequired()
                    uploadDependencies.each { dep, required ->
                        if (required) {
                            requiredContainer.project(dep)
                        } else {
                            optionalContainer.project(dep)
                        }
                    }
                }
            }
        }

        if (env.CURSEFORGEKEY) {
            task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
                apiToken = env.CURSEFORGEKEY
                int projectId = uploadSettings["curse-id"] as int

                def mainFile = upload(projectId, tasks.remapJar)
                mainFile.changelog = changeLogText
                mainFile.changelogType = "markdown"

                if (name == "fabric") {
                    mainFile.releaseType = "beta"
                }
                else {
                    mainFile.releaseType = "release"
                }

                mainFile.displayName = "Repurposed Structures v${project.mod_version} (${project.minecraft_version} MC ${name.capitalize()})"
                mainFile.addGameVersion("${rootProject.minecraft_version}")
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = "${rootProject.mod_version}+${rootProject.minecraft_version}-${name}"
    group = rootProject.maven_group

    repositories {
        maven {
            url = 'https://maven.cafeteria.dev/releases'
            content {
                includeGroup 'dev.cafeteria'
            }
        }
        maven { url "https://nexus.resourcefulbees.com/repository/telepathicgrunt/" }
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://maven.shedaniel.me" }
        maven { url "https://jitpack.io" }
        maven { url "https://www.cursemaven.com" }
        maven { url "https://api.modrinth.com/maven/" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}