buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
	}
}

plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id "net.darkhax.curseforgegradle" version "1.0.7"
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = "${project.mod_version}+${project.minecraft_version}"
archivesBaseName = "${project.modid}_fabric"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/repurposed_structures.accesswidener")
}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://api.modrinth.com/maven/" }
	maven {	url "https://nexus.resourcefulbees.com/repository/telepathicgrunt/" }
	maven { url "https://www.cursemaven.com" }
	maven {
		url 'https://jitpack.io'
		content {
			includeGroup 'com.github.llamalad7.mixinextras'
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()

	include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixin_extra_version}")))

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation ("maven.modrinth:modmenu:${project.mod_menu_version}") { transitive = false }

	modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"
	modRuntimeOnly "maven.modrinth:midnightlib:${project.midnightlib_version}"

	modRuntimeOnly "com.telepathicgrunt:CommandStructures-Fabric:${project.commandstructures_version}"
	modRuntimeOnly "com.telepathicgrunt:StructureVoidToggle-Fabric:${project.structurevoidtoggle_version}"

	//modRuntimeOnly "curse.maven:friendsandfoes-551364:3827720"
	//modRuntimeOnly "curse.maven:architecturyapi-419699:3834396"

	//modRuntimeOnly "curse.maven:betternether-311377:3814362"
	//modRuntimeOnly "curse.maven:betterend-413596:3814370"
	//modRuntimeOnly "curse.maven:bclib-495191:3814359"

	//modRuntimeOnly "curse.maven:cyanide-543444:3538092"
	//modRuntimeOnly "curse.maven:towersofthewild-552655:3633073"
	//modRuntimeOnly "curse.maven:fabric_waystones-410902:3479213"
	//modRuntimeOnly "curse.maven:more_villagers-486080:3480631"
	//modRuntimeOnly "curse.maven:farmers_delight-482834:3466296"
	//modRuntimeOnly "curse.maven:better_strongholds-480684:3479839"
	//modRuntimeOnly "curse.maven:yungs_api-421649:3479840"
}

apply from: 'gradle/processresources.gradle'

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

apply from: 'gradle/maven.gradle'

String fileContents = new File("CHANGELOG.md").getText('UTF-8')

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = "$System.env.CURSEFORGEKEY"
	int projectId = 391366

	def mainFile = upload(projectId, remapJar)
	mainFile.changelog = fileContents
	mainFile.changelogType = "markdown"
	mainFile.releaseType = "release"
	mainFile.displayName = "Repurposed Structures v${project.mod_version} (${project.minecraft_version} MC Fabric)"
	mainFile.addRequirement("fabric-api")
	mainFile.addRequirement("midnightlib")
	mainFile.addGameVersion("1.19")
	mainFile.addGameVersion("1.19.1")
	mainFile.addGameVersion("1.19.2")
}

modrinth {
	token = System.getenv("MODRINTH")
	versionName = "Repurposed Structures"
	versionNumber = project.version + "+fabric"
	projectId = "muf0XoRe"
	uploadFile = remapJar
	changelog = fileContents
	versionType = "RELEASE"
	gameVersions = ["1.19"]
	gameVersions = ["1.19.1"]
	gameVersions = ["1.19.2"]
	loaders = ["fabric"]
	dependencies {
		required.project "fabric-api"
		required.project "midnightlib"
	}
}


task buildAndRelease{}
tasks.build.mustRunAfter tasks.clean
tasks.publish.mustRunAfter tasks.build
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.modrinth.mustRunAfter tasks.publish
tasks.publishCurseForge.dependsOn("remapJar")
tasks.buildAndRelease.dependsOn(tasks.clean, tasks.build, tasks.publish, tasks.publishCurseForge, tasks.modrinth)