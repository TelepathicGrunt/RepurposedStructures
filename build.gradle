buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.2.1'
    }
}

plugins {
    id "net.darkhax.curseforgegradle" version "1.0.7"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = "${mod_version}+${mc_version}"
group = "com.telepathicgrunt"
archivesBaseName = "${modid}_forge"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: "official", version: "${mc_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
   
    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=" + project.getProperty('modid') + ".mixins.json"
            mods { repurposed_structures { source sourceSets.main } }
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=" + project.getProperty('modid') + ".mixins.json"
            mods { repurposed_structures { source sourceSets.main } }
        }

        data {
            workingDirectory project.file('run')
            args '--mod', project.getProperty('modid'), '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            arg "-mixin.config=" + project.getProperty('modid') + ".mixins.json"
            mods { repurposed_structures { source sourceSets.main } }
        }
    }
}

repositories {
    maven { url "https://nexus.resourcefulbees.com/repository/maven-public/" }
    maven { url "https://www.cursemaven.com" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    runtimeOnly fg.deobf("com.telepathicgrunt:CommandStructures:2.0.1+1.18.2")
    runtimeOnly fg.deobf("com.telepathicgrunt:FeatureNBTDeadlockBeGone-Forge:2.0.0+1.18.2")
    runtimeOnly fg.deobf("curse.maven:structure_void_toggle-357304:3644532")
    compileOnly fg.deobf("curse.maven:async_locator-625069:3816310") // v1.0.1
    //runtimeOnly fg.deobf("curse.maven:advanced_peripherals-431725:3799046")
    //runtimeOnly fg.deobf("curse.maven:cc_tweaked-282001:3770724")
    //runtimeOnly fg.deobf("curse.maven:the_twilight_forest-227639:3818688")

    // 1.18.2
    //runtimeOnly fg.deobf("curse.maven:pneumaticcraft_repressurized-281849:3682893")
    //runtimeOnly fg.deobf("curse.maven:patchouli-306770:3680301")
    //runtimeOnly fg.deobf("curse.maven:waystones-245755:3684732")
    //runtimeOnly fg.deobf("curse.maven:balm-531761:3710117")

    // 1.18.1
    //runtimeOnly fg.deobf("curse.maven:better_strongholds-465575:3650285")
    //runtimeOnly fg.deobf("curse.maven:better_dungeons-510089:3659567")
    //runtimeOnly fg.deobf("curse.maven:yungs_api-421850:3652542")
    //runtimeOnly fg.deobf("curse.maven:endremastered-404183:3600701")
    //runtimeOnly fg.deobf("curse.maven:terralith-513688:3606078")
    //runtimeOnly fg.deobf("curse.maven:morevillagers-484954:3548004")
    //runtimeOnly fg.deobf("curse.maven:hexerei-548599:3562165")
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}

apply from: 'gradle/processresources.gradle'
apply from: 'gradle/manifest.gradle'
apply from: 'gradle/maven.gradle'
apply from: 'gradle/modrinth.gradle'
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = "$System.env.CURSEFORGEKEY"
    int projectId = 368293
    String changelogText = new File("CHANGELOG.md").getText('UTF-8')

    def mainFile = upload(projectId, jar)
    mainFile.changelog = changelogText
    mainFile.changelogType = "markdown"
    mainFile.releaseType = "release"
    mainFile.displayName = "Repurposed Structures v${project.mod_version} (${project.mc_version} MC)"
    mainFile.addRequirement("feature-nbt-deadlock-be-gone")
    mainFile.addOptional("async-locator")
}

task checkForRefmap() {
    if (file("./build/libs/repurposed_structures_forge-${project.mod_version}+${project.mc_version}.jar").exists()) {
        assert zipTree("./build/libs/repurposed_structures_forge-${project.mod_version}+${project.mc_version}.jar")
                .matching { include "repurposed_structures.refmap.json" }.getSingleFile() != null
    }
}

task buildAndRelease{}
tasks.checkForRefmap.mustRunAfter tasks.build
tasks.publish.mustRunAfter tasks.checkForRefmap
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.publishModrinth.mustRunAfter tasks.publish
tasks.buildAndRelease.dependsOn(tasks.build, tasks.checkForRefmap, tasks.publish, tasks.publishCurseForge, tasks.publishModrinth)