plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    common("fabric", "quilt", "forge")
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}") {
        force = true
    }

    modRuntimeOnly("com.telepathicgrunt:CommandStructures-Fabric:${project.commandstructures_fabric}")
    modRuntimeOnly("com.telepathicgrunt:StructureVoidToggle-Fabric:${project.structurevoidtoggle_fabric}")

    modImplementation "maven.modrinth:midnightlib:${rootProject.midnightlib}"

    modCompileOnly "maven.modrinth:modmenu:${rootProject.mod_menu}"
    modRuntimeOnly "maven.modrinth:modmenu:${rootProject.mod_menu}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }

    include "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
    modImplementation "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
    annotationProcessor "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}